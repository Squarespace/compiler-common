
plugins {
  id 'me.champeau.gradle.jmh' version '0.2.0'
}

description = 'Compiler Common core'

configurations {
  tests {
    extendsFrom testRuntime
  }
  animalSniffer
  javaApiSignature
}

dependencies {
  testCompile "org.testng:testng:${testngVersion}"
  testCompile "org.hamcrest:java-hamcrest:${hamcrestVersion}"

  animalSniffer("org.codehaus.mojo:animal-sniffer-ant-tasks:${snifferVersion}")
  javaApiSignature("org.codehaus.mojo.signature:java18:1.0@signature")
}

artifacts {
  archives sourcesJar
  archives testJar
  archives javadocJar
  tests testJar
}

jmh {
  jmhVersion = '1.11.1'
  forceGC = true
}

plugins.withType(EclipsePlugin) {
  project.eclipse.classpath.plusConfigurations += [ configurations.jmh ]
}

task copyJavaApiSignature(type: Copy) {
    from configurations.javaApiSignature
    into "$buildDir/javaApiSignature/"
    rename '.*signature', 'javaApi.signature'
}

task checkJavaApiSignature << {
    ant.taskdef(
      name: 'animalSniffer',
      classname: 'org.codehaus.mojo.animal_sniffer.ant.CheckSignatureTask',
      classpath: configurations.animalSniffer.asPath
    )
    ant.animalSniffer(
      signature: "$buildDir/javaApiSignature/javaApi.signature",
      classpath: configurations.compile.asPath
    ) { path(path: "$buildDir/classes/main") }
}

checkJavaApiSignature.dependsOn compileJava
checkJavaApiSignature.dependsOn copyJavaApiSignature
check.dependsOn checkJavaApiSignature

